plugins {
    // Apply the groovy plugin to add support for Groovy
    id 'groovy'
    // Apply Gradle JPI plugin to simplify dependency management
    id 'org.jenkins-ci.jpi' version '0.39.0'
}

def testLogLevel = project.findProperty('testLogLevel') ?: 'ERROR'

group 'com.example'
version 'O.1-SNAPSHOT'

allprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven { url 'https://repo.jenkins-ci.org/releases/' }
}

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:2.5.10'

    testImplementation 'com.homeaway.devtools.jenkins:jenkins-spock:2.1.2'
    testImplementation 'ch.qos.logback:logback-core:1.2.3'
    testImplementation 'ch.qos.logback:logback-classic:1.2.3'
    testImplementation 'javax.servlet:javax.servlet-api:3.1.0'
    testImplementation 'org.jenkins-ci.main:jenkins-core:2.190.2', {
        exclude group: 'com.ibm.icu', module: 'icu4j'
    }
    // provides stage() step
    testImplementation 'org.jenkins-ci.plugins:pipeline-stage-step:2.3'
    // provides sh() step
    testImplementation 'org.jenkins-ci.plugins.workflow:workflow-durable-task-step:2.35'
    // provides GlobalVariable
    testImplementation 'org.jenkins-ci.plugins.workflow:workflow-cps-global-lib:2.10'
    // provides slackSend() step
    testImplementation 'org.jenkins-ci.plugins:slack:2.3'
    // provides sshagent() step
    testImplementation 'org.jenkins-ci.plugins:ssh-agent:1.16'

    // plugin dependencies
    testImplementation 'com.cloudbees:groovy-cps:1.31'  // required by workflow-cps plugin
}

task cleanSharedLibraries(type: Delete) {
    delete "$buildDir/classes/groovy/test/vars"
}

task prepareSharedLibraries(type: Copy) {
    dependsOn tasks.cleanSharedLibraries
    from '.'
    include 'Jenkinsfile'
    into "$buildDir/classes/groovy/test/vars"
}

test {
    dependsOn tasks.prepareSharedLibraries
    systemProperties = [
            'root.loglevel': testLogLevel,
            'root.appender': 'Stdout',
            'logdir': project.buildDir
    ]
}
